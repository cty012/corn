# Paths
set(Corn_ROOT "${CMAKE_CURRENT_LIST_DIR}/..")

set(Corn_RUNTIME_DIR "${Corn_ROOT}/bin")
set(Corn_INCLUDE_DIR "${Corn_ROOT}/include")
set(Corn_LIB_DIR "${Corn_ROOT}/lib")

# Provide these variables to the calling project
include(FindPackageHandleStandardArgs)
if (WIN32)
    set(Corn_DYNAMIC_LIBRARY_DEBUG "${Corn_RUNTIME_DIR}/corn-d.dll")
    set(Corn_DYNAMIC_LIBRARY_RELEASE "${Corn_RUNTIME_DIR}/corn.dll")
    set(Corn_IMPORT_LIBRARY_DEBUG "${Corn_ROOT}/lib/corn-d.lib")
    set(Corn_IMPORT_LIBRARY_RELEASE "${Corn_ROOT}/lib/corn.lib")
    find_package_handle_standard_args(Corn DEFAULT_MSG
            Corn_DYNAMIC_LIBRARY_DEBUG
            Corn_DYNAMIC_LIBRARY_RELEASE
            Corn_IMPORT_LIBRARY_DEBUG
            Corn_IMPORT_LIBRARY_RELEASE
            Corn_INCLUDE_DIR
    )
elseif (APPLE)
    set(Corn_DYNAMIC_LIBRARY_DEBUG "${Corn_RUNTIME_DIR}/libcorn-d.dylib")
    set(Corn_DYNAMIC_LIBRARY_RELEASE "${Corn_RUNTIME_DIR}/libcorn.dylib")
    find_package_handle_standard_args(Corn DEFAULT_MSG
            Corn_DYNAMIC_LIBRARY_DEBUG
            Corn_DYNAMIC_LIBRARY_RELEASE
            Corn_INCLUDE_DIR
    )
else ()
    set(Corn_DYNAMIC_LIBRARY_DEBUG "${Corn_RUNTIME_DIR}/libcorn-d.so")
    set(Corn_DYNAMIC_LIBRARY_RELEASE "${Corn_RUNTIME_DIR}/libcorn.so")
    find_package_handle_standard_args(Corn DEFAULT_MSG
            Corn_DYNAMIC_LIBRARY_DEBUG
            Corn_DYNAMIC_LIBRARY_RELEASE
            Corn_INCLUDE_DIR
    )
endif ()

if(Corn_FOUND AND NOT TARGET Corn::Corn)
    add_library(Corn::Corn SHARED IMPORTED)
    set_target_properties(Corn::Corn PROPERTIES
            IMPORTED_LOCATION "${Corn_DYNAMIC_LIBRARY_RELEASE}"
            IMPORTED_LOCATION_DEBUG "${Corn_DYNAMIC_LIBRARY_DEBUG}"
            IMPORTED_LOCATION_RELEASE "${Corn_DYNAMIC_LIBRARY_RELEASE}"
            INTERFACE_INCLUDE_DIRECTORIES "${Corn_INCLUDE_DIR}"
    )

    if (WIN32)
        set_target_properties(Corn::Corn PROPERTIES
                IMPORTED_IMPLIB "${Corn_LIBRARIES_RELEASE}"
                IMPORTED_IMPLIB_DEBUG "${Corn_IMPORT_LIBRARY_DEBUG}"
                IMPORTED_IMPLIB_RELEASE "${Corn_IMPORT_LIBRARY_RELEASE}"
        )
    endif ()

    if (CMAKE_BUILD_TYPE STREQUAL "Debug")
        set(Corn_DYNAMIC_LIBRARY "${Corn_DYNAMIC_LIBRARY_DEBUG}")
    else ()
        set(Corn_DYNAMIC_LIBRARY "${Corn_DYNAMIC_LIBRARY_RELEASE}")
    endif ()
endif()
